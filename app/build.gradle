apply plugin: 'com.android.application'

android {
    signingConfigs {
        config {
        }
    }
    compileSdkVersion 23
    buildToolsVersion '23.0.3'
    defaultConfig {
        applicationId "com.nbusy.app"
        minSdkVersion 22
        targetSdkVersion 23
        versionCode 0
        versionName '0'
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    lintOptions {
        abortOnError false
        warning 'InvalidPackage' // hack: due to okio (okhttp dependency)
    }
    adbOptions { // (Optional) This enables long timeouts required on slow environments, e.g. Travis
        timeOutInMs 5 * 60 * 1000  // 5 minutes
        installOptions "-d", "-t"
    }
    productFlavors {
    }
}

if (System.env.NBUSY_VERSION_CODE && System.env.NBUSY_VERSION_NAME) {
    android.defaultConfig.versionCode = System.env.NBUSY_VERSION_CODE
    android.defaultConfig.versionName = System.env.NBUSY_VERSION_NAME
}

if (System.env.NBUSY_KEYSTORE) {
    android.signingConfigs.config.storeFile = file(System.env.NBUSY_KEYSTORE)
    android.signingConfigs.config.keyAlias = System.env.NBUSY_KEYSTORE_ALIAS
    android.signingConfigs.config.keyPassword = System.env.NBUSY_KEYSTORE_PASS
    android.signingConfigs.config.storePassword = System.env.NBUSY_KEYSTORE_PASS
    android.buildTypes.release.signingConfig = android.signingConfigs.config
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')

    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:1.10.19'
    testCompile 'org.hamcrest:hamcrest-library:1.3'

    androidTestCompile 'com.android.support:support-annotations:23.4.0'
    androidTestCompile 'com.android.support.test:runner:0.5' // Android JUnit Runner
    androidTestCompile 'com.android.support.test:rules:0.5' // JUnit4 Rules
    androidTestCompile 'com.android.support.test.espresso:espresso-core:2.2.2'
//    androidTestCompile 'com.android.support.test.espresso:espresso-contrib:2.2.2' // Espresso-contrib for DatePicker, RecyclerView, Drawer actions, Accessibility checks, CountingIdlingResource
//    androidTestCompile 'com.android.support.test.espresso:espresso-web:2.2.2' // Espresso-web for WebView support
//    androidTestCompile 'com.android.support.test.espresso:espresso-idling-resource:2.2.2' // Espresso-idling-resource for synchronization with background jobs

    compile 'com.android.support:support-v4:23.4.0'
    compile 'com.android.support:appcompat-v7:23.4.0'
    compile 'com.google.android.gms:play-services-gcm:8.4.0'
    compile 'com.google.android.gms:play-services-auth:8.4.0'
    compile 'com.google.guava:guava:19.0'
    compile 'com.google.code.gson:gson:2.6.2'
    compile 'com.squareup.okhttp3:okhttp:3.2.0'
    compile 'com.squareup.okhttp3:okhttp-ws:3.2.0'
}

apply plugin: 'com.google.gms.google-services'
